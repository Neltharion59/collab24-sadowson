//////////////////////////////////////////////////////////////////////////////
//
// File     :  collab_ss_mercenary_skins.skrit
// Author(s):  Sadowson
//
// Copyright Â© 2024 Gas Powered Games, Inc.  All rights reserved.
//----------------------------------------------------------------------------
//  $Revision:: $              $Date:$
//----------------------------------------------------------------------------
//////////////////////////////////////////////////////////////////////////////
//Reserved Tags
property 	bool	_server_only$	= false;
property	string	_doc$		= "Assigns random skins to the mercenary on creation.";

//Properties

property	string	actor_class$				doc = "Valid types are MELEE, RANGED, CMAGIC, NMAGIC";

string				male_head_prefix$	= "m_c_gah_fb_hlmt_";
string				male_head_types$	= "EMPTY,type33";
string				male_skin_prefix$	= "b_c_gah_fb_skin_";
string				male_skin_types$	= "NONE,01,02,03,04,05,06,07,08,09,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,26,27,28,29,30,31,32,33,36,37,38";

string				female_head_prefix$	= "m_c_gah_fg_hlmt_";
string				female_head_types$	= "EMPTY,head-02,head-03,head-04,head-05";
string				female_skin_prefix$	= "b_c_gah_fg_skin_";
string				female_skin_types$	= "EMPTY,01,02,03,04,05,06,07,08,09,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24";

string				melee_helmets$		= ",type9,type13,type19,type20,type22,type24";
string				ranged_helmets$		= ",type6,type8,type13,type14,type20";
string				cmagic_helmets$		= ",type6,type8,type15,type16,type36";
string				nmagic_helmets$		= ",type6,type10,type15,type26,type35,type36";

string				head_prefix$;
string				head_type$;
string				skin_prefix$;
string				skin_type$;

string				assigned_head$;
string				assigned_skin$;

bool				can_change_head$	= false;
bool				can_change_skin$	= false;

bool		is_male$;

owner = GoSkritComponent;

//////////////////////////////////////////////////////////////////////////////

event OnGoHandleMessage$( eWorldEvent e$, WorldMessage )
{
	if( e$ == WE_ENTERED_WORLD )
	{
		if( owner.go.Actor.IsMale )
		{
			is_male$ = true;
			
			head_prefix$ = male_head_prefix$;
			head_type$ = male_head_types$;
			skin_prefix$ = male_skin_prefix$;
			skin_type$ = male_skin_types$;
		}
		else
		{
			is_male$ = false;
			
			head_prefix$ = female_head_prefix$;
			head_type$ = female_head_types$;
			skin_prefix$ = female_skin_prefix$;
			skin_type$ = female_skin_types$;
		}
		
		//The system seems to have a clinical bias towards lower RNG numbers, so we double the listings to double the chance of a helmet, since we want their chances of NOT having a helmet to be 1/3, not 1/2.
		if( actor_class$ == "MELEE" )
		{
			StringTool.Append( head_type$, melee_helmets$ );
			StringTool.Append( head_type$, melee_helmets$ );
		}
		else if( actor_class$ == "RANGED" )
		{
			StringTool.Append( head_type$, ranged_helmets$ );
			StringTool.Append( head_type$, ranged_helmets$ );
		}
		else if( actor_class$ == "CMAGIC" )
		{
			StringTool.Append( head_type$, cmagic_helmets$ );
			StringTool.Append( head_type$, cmagic_helmets$ );
		}
		else if( actor_class$ == "NMAGIC" )
		{
			StringTool.Append( head_type$, nmagic_helmets$ );
			StringTool.Append( head_type$, nmagic_helmets$ );
		}
		
		
		
		int Rand$;
		int Top_Rand$ = StringTool.GetNumDelimitedValues( head_type$ );
		Rand$ = GoMath.RandomInt( 1, Top_Rand$-1 );
		if( Rand$ == 1 )
		{
			can_change_head$ = false;
		}
		else
		{
			can_change_head$ = true;
			head_type$ = StringTool.GetDelimitedString( head_type$, Rand$ );
			assigned_head$ = head_prefix$;
			StringTool.Append( assigned_head$, head_type$ );
		}
		
		
		Top_Rand$ = StringTool.GetNumDelimitedValues( skin_type$ );
		Rand$ = GoMath.RandomInt( 1, Top_Rand$-1 );
		if( Rand$ == 1 )
		{
			can_change_skin$ = false;
		}
		else
		{
			can_change_skin$ = true;
			skin_type$ = StringTool.GetDelimitedString( skin_type$, Rand$ );
			assigned_skin$ = skin_prefix$;
			StringTool.Append( assigned_skin$, skin_type$ );
		}
		
		//QA note: If the skin changes occur after the custom_head is applied, then it will do really funky things to their skins and not work. Remember to set the assigned skin BEFORE the custom head!
		if( can_change_skin$ )
		{
			owner.go.Aspect.SSetCurrentAspectTexture( 0, assigned_skin$ );
			Report.GenericF( "collab_ss_mercenary_skins: Assigned a %s skin to this actor. \n", assigned_skin$ );
		}
		else
		{
			Report.GenericF( "Collab_ss_mercenary_skins: Made no change to the skin. \n" );
		}
		
		if( can_change_head$ )
		{
				owner.go.Inventory.RCSetCustomHead( assigned_head$ );
				Report.GenericF( "collab_ss_mercenary_skins: Assigned a %s head to this actor \n", assigned_head$ );
		}
	}
}