/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ee_seraphine.skrit

	Author(s)	: 	Sadowson

	Purpose		: 	Generic script which checks the actors highest skill level and then activates the relevant conversation.

	(C)opyright 2024 Sadowson.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Face$;
}

////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// $$$ read/write variables from the Game Auditor here, and bring up the appropriate dialogs...
		
		float player_nature_magic$ = m_target$.Go.Actor.GetSkillLevel( "Nature Magic" );
		float player_combat_magic$ = m_target$.Go.Actor.GetSkillLevel( "Nature combat_magic" );
		float player_melee$ = m_target$.Go.Actor.GetSkillLevel( "Melee" );
		float player_ranged$ = m_target$.Go.Actor.GetSkillLevel( "Ranged" );
		
		string player_highest_stat$ = "NONE";
		string conversation_block$ = "conversation_seraphine_";
		string quest_name$ = "quest_warding_crystal";
		
		//Check if the quest has been completed.
		if( Victory.IsQuestCompleted( quest_Name$ ) )
		{
			StringTool.Append( conversation_block$, "complete_" );
		}
		else
		{
			StringTool.Append( conversation_block$, "hello_" );
		}
		
		//Check for the targets highest stat.
		float max_value$ = 0;
		if( player_nature_magic$ > max_value$ )
		{
			max_value$ = player_nature_magic$;
			player_highest_stat$ = "nature";
		}
		if( player_combat_magic$ > max_value$ )
		{
			max_value$ = player_combat_magic$;
			player_highest_stat$ = "combat";
		}
		if( player_melee$ > max_value$ )
		{
			max_value$ = player_melee$;
			player_highest_stat$ = "melee";
		}
		if( player_ranged$ > max_value$ )
		{
			max_value$ = player_ranged$;
			player_highest_stat$ = "ranged";
		}
		StringTool.Append( conversation_block$, player_highest_stat$ );
		
		//Use Conversation
		m_go$.GetConversation.RSSelectConversation( conversation_block$ );
		TryAnim$('talk');
		
		//Start talking
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


